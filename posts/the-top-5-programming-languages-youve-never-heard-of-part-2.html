<head>
  <title>The top 5 programming languages you've never heard of (part 2)</title>
  <script src="/bower_components/webcomponentsjs/webcomponents-lite.js">
  </script>
  <link rel="import" href="/_base.html">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
</head>

<body>
  <div id="page">
    <site-navbar></site-navbar>
    <site-title created-on="2015-03-08 17:04"></site-title>

    <div id="teaser">
      <p>
        Yesterday, I wrote a post about
        <a href="/posts/the-top-5-programming-languages-youve-never-heard-of.html">
          the top 5 programming languages you've never heard of</a>.
        Well, since it was kind of short (I didn't have too much time to write), I'm
        going to put a little more info here.</p>
    </div>

    <link-header id="nim">Nim</link-header>

    <p>
      I enjoy coding in <a href="http://nim-lang.org/">Nim</a> because it's fun.
      Nim's goal is seemingly to blur the line between compiled and scripting
      languages, and it's doing a GREAT job. The compiler is self-hosting, but it's
      not quite the prime example of a well-written Nim program (it was ported from
      Object Pascal). A better example is Nimble: the Nim package manager. Here's a
      piece of Nimble's source code:</p>

    <prism-js language="nim">
proc update(options: Options) =
  ## Downloads the package list from the specified URL.
  ##
  ## If the download is successful, the global didUpdatePackages is set to
  ## true. Otherwise an exception is raised on error.
  let url =
    if options.action.typ == actionUpdate and options.action.optionalURL != "":
      options.action.optionalURL
    else:
      defaultPackageURL
  echo("Downloading package list from " &amp; url)
  downloadFile(url, options.getNimbleDir() / "packages.json")
  echo("Done.")
    </prism-js>

    <p>A longer piece:</p>

    <prism-js language="nim">
proc parseConfig*(): Config =
  result = initConfig()
  var confFile = getConfigDir() / "nimble" / "nimble.ini"

  var f = newFileStream(confFile, fmRead)
  if f == nil:
    # Try the old deprecated babel.ini
    confFile = getConfigDir() / "babel" / "babel.ini"
    f = newFileStream(confFile, fmRead)
    if f != nil:
      echo("[Warning] Using deprecated config file at ", confFile)

  if f != nil:
    echo("Reading from config file at ", confFile)
    var p: CfgParser
    open(p, f, confFile)
    while true:
      var e = next(p)
      case e.kind
      of cfgEof:
        break
      of cfgSectionStart: discard
      of cfgKeyValuePair, cfgOption:
        case e.key.normalize
        of "nimbledir":
          # Ensure we don't restore the deprecated nimble dir.
          if e.value != getHomeDir() / ".babel":
            result.nimbleDir = e.value
        of "chcp":
          result.chcp = parseBool(e.value)
        else:
          raise newException(NimbleError, "Unable to parse config file:" &
                                  " Unknown key: " &amp; e.key)
      of cfgError:
        raise newException(NimbleError, "Unable to parse config file: " &amp; e.msg)
    close(p)
    </prism-js>

    <link-header id="felix">Felix</link-header>

    <p>
      <a href="http://felix-lang.org/">Felix</a> is unique. It's a mix of C++, ML,
      and lots of unique ideas. Here's a nice sample from a toy JSON parser I was
      writing (this is only the lexer part):</p>

    <prism-js>
class JSON {
    typedef LineType = int;

    union Value =
        | Object of strdict[Value]
        | Array  of list[Value]
        | String of string
        | Number of double
        | True
        | False
        | Null
        | Error of string * LineType
    ;

    union Token =
        | TString of string
        | TNumber of double
        | TLBrace // {
        | TRBrace // }
        | TLBrak  // [
        | TRBrak  // ]
        | TColon  // :
        | TTrue   // true
        | TFalse  // false
        | TNull   // null
        | TEOF
        | TError of string * LineType
    ;

    instance Str[Token] {
        fun str(t: Token) =&gt; match t with
            | TString ?s =&gt; "TString \"" + s + "\""
            | TNumber ?n =&gt; "TNumber " + n.str
            | TLBrace    =&gt; "TLBrace"
            | TRBrace    =&gt; "TRBrace"
            | TLBrak     =&gt; "TLBrak"
            | TRBrak     =&gt; "TRBrak"
            | TColon     =&gt; "TColon"
            | TTrue      =&gt; "TTrue"
            | TFalse     =&gt; "TFalse"
            | TNull      =&gt; "TNull"
            | TEOF       =&gt; "TEOF"
            | TError (?s, ?i) =&gt; "error at line " + i.str + ": " + s
        endmatch;
    }

    proc lex(s: string, line: &LineType, outs: oschannel[Token]) = {
        line <- 1;

        proc tok(t: Token) =&gt; write $ outs, t;

        proc err(s: string) = {
            tok $ TError(s, *line);
            return from lex;
        };

        var i = 0.size;

        while i &lt; s.len do
            while s.[i].isspace do
                if s.[i] == "\n" do *line++; done;
                i++;
                if i >= s.len goto eof;
            done;

            // number
            if s.[i].isnumeric or (i+1 &lt; s.len and s.[i] == "-" and
                                     s.[i+1].isnumeric) do
                d := s.[i to].double;
                i += d.str.len;
                tok $ TNumber d;
            // string
            elif s.[i] == "\"" do
                i++;
                var st = "";
                while i &lt; s.len and s.[i] != "\n" and s.[i] != "\"" do
                    st += s.[i];
                    i++;
                done;
                if s.[i] != "\"" call err "unterminated string literal";
                i++;
                tok $ TString st;
            // literals
            elif s.[i to i+4] == "true" do
                tok $ TTrue;
                i += 4.size;
            elif s.[i to i+5] == "false" do
                tok $ TFalse;
                i += 5.size;
            elif s.[i to i+4] == "null" do
                tok $ TNull;
                i += 4.size;
            // others
            else
                match s.[i].str with
                    | "{" =&gt; tok TLBrace;
                    | "}" =&gt; tok TRBrace;
                    | "[" =&gt; tok TLBrak;
                    | "]" =&gt; tok TRBrak;
                    | ":" =&gt; tok TColon;
                    | _   =&gt; err "unknown token";
                endmatch;

                i++;
            done;
        done;

        eof:>
        tok TEOF;
    }
}
    </prism-js>

    <p>
      <a href="http://felix-lang.org/%24/usr/local/lib/felix/felix-latest//share/lib/web/json.flx">
        Here's</a> a link to Felix's own JSON parser, which is written more nicely
        than mine is...</p>

    <p>
      It illustrates some nice features, such as schannels (coroutines on steroids).
      schannels are like Go's channels, but not concurrent. Felix has another Go-like
      channel named fchannels, which are concurrent.</p>

    <p>
      Felix also has a nice set of utilities (a web server, a literate programming
      format, an
      <a href="https://github.com/felix-lang/felix/blob/master/src/tools/flx_config.fdoc">
        alpha-quality</a> graphical config tool) and a decently-sized standard
        library.</p>

    <p>
      Cons? Very little documentation. However, the mailing list is very
      responsive.</p>

    <link-header id="myrddin">Myrddin</link-header>

    <p>
      <a href="http://eigenstate.org/myrddin/">Myrddin</a> is essentially how C
      would probably look if it were designed right now. Some features:</p>

    <ul>
      <li>Type inference</li>
      <li>Pattern matching</li>
      <li>Go-style slices</li>
      <li>C-style memory management</li>
    </ul>

    <p>
      All in a C-style language. I've been toying with writing a kernel in it, and
      it's been going very well. I have a lot of hope in Myrddin.</p>

    <p>
      A great example is the
      <a href="http://git.eigenstate.org/ori/libbio.git/tree/bio.myr">libbio</a>
      input/output library. Here's a snippet:</p>

    <prism-js>
/*
writes to as much from `src` as possible to a file,
returning the number of bytes written.
*/
const write = {f, src
    std.assert(f.mode &amp; Wr != 0, "File is not in write mode")
    /*
    Tack small writes onto the buffer end. Big ones
    flush the buffer and then go right to kernel.
    */
    if src.len &lt; (f.wbuf.len - f.wend)
        std.slcp(f.wbuf[f.wend:f.wend+src.len], src)
        f.wend += src.len
        -&gt; src.len
    else
        flush(f)
        -&gt; writebuf(f.fd, src)
    ;;
}
    </prism-js>

    <p>
      However, the compiler generates VERY slow x64 assembly code at the
      moment...and only x64 assembly code. I'm working on a C backend, though, but
      it'll be a while until it's finished.</p>

    <link-header id="k">K</link-header>

    <p>
      <a href="http://www.kuro5hin.org/story/2002/11/14/22741/791">K</a>, along with
      <a href="http://kona.github.io/">Kona</a> (an open-source K implementation
      with bad error messages) is special. It's the result of shoving APL into an
      ASCII-character world.</p>

    <p>
      Here are some of the idioms at the
      <a href="https://github.com/kevinlawler/kona/wiki/Idioms">Kona wiki</a>:</p>

    <prism-js escape>
shuffle:{x@<>(#x)#1 0} / Perfect shuffle
mean:{(+/x)%#x} / Arithmetic mean
fac:*/1+!: / Factorial
fib:{x{x,+/-2#x}/!2} / Fibonacci
life:{|/(1;x)&3 4=\:+/,/2{-1 0 1!'\:x}/x} / Conway's Game of Life
sort:{x@&lt;x} / Sort list
powerset:{x[&:'!2+&#x]} / Powerset
    </prism-js>

    <p>
      As you can see, K is very concise. A little too concise. However, as an
      array-processing language, it's great for iterating over large sequences of
      data, as <a href="http://kx.com/">kdb+</a> has shown. It's also very fast and
      lean (I'm speaking for kdb+, though, not Kona).</p>

    <p>
      If you can't already tell, the main con is that no one that doesn't know K
      will be able to read your programs without getting a seizure.</p>

    <link-header id="objeck">Objeck</link-header>

    <p>
      As I said before, <a href="http://www.objeck.org/">Objeck</a> is kind of like
      I envisioned Java to be, other than the (painful) lack of generics.</p>

    <p>It's pretty expressive and nice to work with:</p>

    <prism-js>
class Factorial {
    function : native : Factorial(n : Int) ~ Int {
        if (n <= 1) {
            return n;
        } else {
            return n * Factorial(n-1);
        };
    }

    function : Main(args : String[]) ~ Nil {
        "Number: "->Print();
        number := IO.Console->ReadString()->ToInt();
        if (number &lt; 0) {
            "Number must be greater than 0"->PrintLine();
            Runtime->Exit(1);
        };
        Factorial(number)->PrintLine();
    }
}
    </prism-js>

    <p>
      It's very Java-esque but nicer to use. The cons would be the fact that there
      is <i>no true native compiler</i>. Sure, there's a "compiler", but it's like
      the Java compiler: it compiles to a bytecode that's executed by a VM. It
      Objeck's case, the VM is <code>obe</code>. You also have to manually specify
      that a function is native for it to be compiled to machine code. There also
      seem to be no unsigned types or operator overloading. Again.</p>

    <link-header id="others">Others</link-header>

    <p>
      Two honorable, discontinued mentions are
      <a href="https://code.google.com/p/anic/">ani</a> and
      <a href="http://www.alorelang.org/">Alore</a>.</p>

    <p>
      Ani is a programming language that would have sported implicit parallelism and
      very nice speed. Note that I said <i>would have</i>. A working compiler was
      never fully completed and the maintainer disappeared, so this language may
      never quite see the light of day. The
      <a href="https://groups.google.com/forum/#!forum/ani-compiler">
        project mailing list</a> is still there, though, so there are hopes that
      someday, someone may indeed restart the project.</p>

    <p>
      Alore was a language based on the ability to freely mix static and dynamic
      typing. Although it was a great idea, the language itself seemingly never
      caught on, and it was abandoned in favor of
      <a href="http://www.mypy-lang.org/">mypy</a>, a project with the same basic
      idea but implemented as a static type checker for Python.</p>

    <link-header id="summary">Summary</link-header>

    <p>
      That's all! As I said before, I hope one of the languages mentioned caught
      your eye.</p>

    <site-tags tags="compilers, felix, k, language, myrddin, nim, objeck, programming">
    </site-tags>
    <site-suffix></site-suffix>
  </div>
</body>
